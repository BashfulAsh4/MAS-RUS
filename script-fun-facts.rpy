#Persistent event database for fun facts
default persistent._mas_fun_facts_database = dict()

init -10 python in mas_fun_facts:
    #The fun facts db
    fun_fact_db = {}

    def getUnseenFactsEVL():
        """
        Gets all unseen (locked) fun facts as eventlabels

        OUT:
            List of all unseen fun fact eventlabels
        """
        return [
            fun_fact_evl
            for fun_fact_evl, ev in fun_fact_db.iteritems()
            if not ev.unlocked
        ]

    def getAllFactsEVL():
        """
        Gets all fun facts regardless of unlocked as eventlabels

        OUT:
            List of all fun fact eventlabels
        """
        return fun_fact_db.keys()


#Whether or not the last fun fact seen was a good fact
default persistent._mas_funfactfun = True

init 5 python:
    addEvent(
        Event(
            persistent.event_database,
            eventlabel="monika_fun_facts_open",
            category=['разное'],
            prompt="Можешь рассказать мне забавный факт?",
            pool=True
        )
    )

label monika_fun_facts_open:
    if mas_getEVL_shown_count("monika_fun_facts_open") == 0:
        m 1eua "Скажи [player], хочешь услышать забавный факт?"
        m 1eub "Я нашла несколько, чтобы попытаться научить нас обоих чему-то новому."
        m 3hub "Говорят, что каждый день ты узнаешь что-то новое, так я хочу убедиться, что мы действительно узнаем."
        m 1rksdla "Я нашла большинство из них в Интернете, поэтому не могу сказать, что они {i}определенно{/i} верны..."

    else:
        m 1eua "Хочешь услышать ещё один забавный факт, [player]?"
        if persistent._mas_funfactfun:
            m 3hua "В конце концов, последний был довольно интересным!"
        else:
            m 2rksdlb "Я знаю, что последний факт был не очень... но я уверена, что следующий будет лучше."
    m 2dsc "Теперь, давай посмотрим.{w=0.5}.{w=0.5}.{nw}"

    python:
        unseen_fact_evls = mas_fun_facts.getUnseenFactsEVL()
        if len(unseen_fact_evls) > 0:
            fact_evl_list = unseen_fact_evls
        else:
            fact_evl_list = mas_fun_facts.getAllFactsEVL()

        #Now we push and unlock the fact
        fun_fact_evl = renpy.random.choice(fact_evl_list)
        mas_unlockEVL(fun_fact_evl, "FFF")
        pushEvent(fun_fact_evl)
    return

#Most labels end here
label mas_fun_facts_end:
    m 3hub "Надеюсь, тебе понравилось очередное занятие 'Учимся с Моникой!'"
    $ persistent._mas_funfactfun = True
    return

label mas_bad_facts_end:
    m 1rkc "Этот факт был не очень хорош..."
    m 4dkc "В следующий раз я постараюсь лучше, [player]."
    $ persistent._mas_funfactfun = False
    return


#START: Good facts
init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_librocubiculartist",
        ),
        code="FFF"
    )

label mas_fun_fact_librocubiculartist:
    m 1eub "Знаешь ли ты, что есть слово, описывающее человека, который любит читать в постели?"
    m 3eub "Это 'либрокубикулярист.' На первый взгляд его трудно произнести."
    m 3rksdld "Очень жаль, что некоторые слова просто не используются в обиходе."
    m 3eud "Но если ты произнесешь это слово, большинство людей не поймут, о чём ты говоришь."
    m 3euc "Тебе, вероятно, придется объяснять, что оно означает, но это как бы уничтожает смысл использования слова."
    m 2rkc "Если бы только люди больше читали и пополняли свой словарный запас!"
    m 2hksdlb "...Э-хе-хе, прости [player]. Я не хотела так беспокоить~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_menu_currency",
        ),
        code="FFF"
    )

label mas_fun_fact_menu_currency:
    m 3euc "Предположительно, многие рестораны специально не указывают в своих меню никакой валюты."
    m 3eud "Это делается для того, чтобы психологически манипулировать людьми, заставляя их тратить больше денег, чем им нужно."
    m 2euc "Это работает, потому что знак валюты, такой как доллар, используется для обозначения стоимости."
    m "Убирая его, ты избавляешься от ассоциаций с этими расходами и думаешь только о выбранной тобой еде."
    m 4rksdld "Эта практика кажется понятной. В конце концов, это всё ещё бизнес."
    m 2dsc "Неважно, насколько хороша еда в ресторане, они быстро закроются, если их победят конкуренты."
    m 3hksdlb "Ну что уж, мы можем поделаем?"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_love_you",
        ),
        code="FFF"
    )

label mas_fun_fact_love_you:
    m 1dkc "Хм, я не уверена, стоит ли мне рассказывать тебе {i}этот{/i} факт."
    m 1ekc "Это не для слабонервных, в конце концов."
    m 1rkc "Дело в том, что..."
    m 1dkc "..."
    m 3hub "...Я люблю тебя, [player]!"
    m 1rksdlb "Э-хе-хе, прости, я просто не могла удержаться."
    m 1hksdlb "В следующий раз у меня будет реальный факт, не волнуйся~"
    #No end for this fact since it ends itself
    $ persistent._mas_funfactfun = True
    return "love"

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_morpheus",
        ),
        code="FFF"
    )

label mas_fun_fact_morpheus:
    m 3wub "О! Языковые факты. Мне всегда такие нравятся."
    m 1eua "Слово 'морфин' основано на греческом боге Морфее."
    m 1euc "Он был греческим богом снов, так что слово, основанное на нем, имеет смысл."
    m 3ekc "Но опять же... разве его отец Гипнос не был богом сна?"
    m 2dsc "Морфий {i}позволяет{/i} человеку видеть сны, но на самом деле он заставляет человека заснуть."
    m 4ekc "...Так не логичнее ли было бы назвать его в честь Гипноса?"
    m 4rksdlb "Слишком рано, слишком поздно, я думаю."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_otter_hand_holding",
        ),
        code="FFF"
    )

label mas_fun_fact_otter_hand_holding:
    m 1eka "Оу, этот очень мило."
    m 3ekb "Знал ли ты, что морские выдры держатся за руки, когда спят, чтобы не дрейфовать друг от друга?"
    m 1hub "Это практично для них, но в этом есть что-то очень милое!"
    m 1eka "Иногда я представляю себя на их месте..."
    m 3hksdlb "Ох, не быть морской выдрой, а держать руку того, кого я люблю, пока я сплю."
    m 1rksdlb "Аха, это действительно заставляет меня завидовать им."
    m 1hub "Когда-нибудь мы добьемся этого, любимый~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_chess",
        ),
        code="FFF"
    )

label mas_fun_fact_chess:
    #Chess is unlocked
    if mas_isGameUnlocked("chess"):
        m 1eua "Вот это интересный факт!"
        m 3eub "Был человек по имени Клод Шеннон, который вычислил максимальное количество возможных ходов в шахматах."
        m "Это число называется 'числом Шеннонаr' и гласит, что количество возможных шахматных партий равно 10^120."
        m 1eua "Его часто сравнивают с количеством атомов в наблюдаемой Вселенной, которое составляет 10^80."
        m 3hksdlb "Немного безумно думать, что шахматных партий может быть больше, чем атомов, не так ли?"
        m 1eua "Мы могли бы играть до конца наших дней, и это не приблизилось бы даже к части того, что возможно."
        m 3eud "Кстати говоря, [player]..."
        m 1hua "Хочешь сыграть со мной партию в шахматы? Возможно, я даже буду полегче с тобой, э-хе-хе~"
        #Call the good end for this path
        call mas_fun_facts_end
        return

    #Chess was unlocked, but locked due to cheating
    elif not mas_isGameUnlocked("chess") and renpy.seen_label("mas_unlock_chess"):
        m 1dsc "Шахматы..."
        m 2dfc "..."
        m 2rfd "Ты можешь забыть об этом факте, так как ты читер, [player]."
        m "Не говоря уже о том, что ты так и не извинился."
        m 2lfc "...Хмф."
        #No end for this path
        return

    #We haven't unlocked chess yet
    else:
        m 1euc "Ох, не этот факт."
        m 3hksdlb "Пока нет, по крайней мере."
        #Call the end
        call mas_bad_facts_end
        return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_struck_by_lightning",
        ),
        code="FFF"
    )

label mas_fun_fact_struck_by_lightning:
    m 2dkc "Хм, это звучит немного некорректно для меня..."
    m 3ekc "'Мужчины в шесть раз чаще попадают под удар молнии, чем женщины.'"
    m 3ekd "Это... довольно глупо, на мой взгляд."
    m 1eud "Если мужчины более склонны к поражению молнией, то, вероятно, ландшафт и обстоятельства их работы делают их более склонными к поражению."
    m 1euc "Мужчины традиционно всегда работали на более опасных и возвышенных работах, так что нет ничего удивительного в том, что это будет происходить с ними чаще."
    m 1esc "Тем не менее, формулировка этого факта заставляет думать, что, будучи мужчиной, это может произойти с большей вероятностью, что просто смешно."
    m 1rksdla "Возможно, если бы это было сформулировано лучше, люди не были бы так дезинформированы о них."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_honey",
        ),
        code="FFF"
    )

label mas_fun_fact_honey:
    m 1eub "Ах, это хороший легкий вариант."
    m 3eub "Знаешь ли ты, что мёд никогда не портится?"
    m 3eua "Хотя мёд может кристаллизоваться. Некоторые люди могут посчитать это порчей, но он всё равно полностью съедобен и прекрасен!"
    m "Это происходит потому, что мёд состоит в основном из сахара и совсем немного из воды, поэтому со временем он становится твёрдым"
    m 1euc "Большинство мёда, который ты видишь в магазинах, не кристаллизуется так быстро, как настоящий мёд, потому что он был пастеризован в процессе производства."
    m 1eud "...Что удаляет вещество, которое заставляет мёд быстро застывать."
    m 3eub "Но разве не приятно было бы есть и кристаллизованный мёд?"
    m 3hub "Это было бы похоже на конфету, когда ты кусаешь её!"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_vincent_van_gone",
        ),
        code="FFF"
    )

label mas_fun_fact_vincent_van_gone:
    m 1dsc "Ах, этот..."
    m 1ekd "Это немного огорчительно, [player]..."
    m 1ekc "Знаешь ли ты, что последними словами Винсента Ван Гога были '{i}La tristesse durera toujours{/i}'?"
    m 1eud "Если перевести, то это означает '{i}Печаль будет длиться вечно.{/i}'"
    m 1rkc "..."
    m 2ekc "Очень грустно осознавать, что кто-то настолько известный сказал что-то настолько мрачное на последнем дыхании."
    m 2ekd "Однако я не думаю, что это правда. Неважно, насколько плохи могут быть дела и насколько глубока может быть печаль..."
    m 2dkc "Наступит время, когда ее больше не будет."
    m 2rkc "...Или, по крайней мере, будет заметно."
    m 4eka "Если тебе когда-нибудь будет грустно, ты ведь знаешь, что можешь поговорить со мной, верно?"
    m 5hub "Я всегда приму и возьму на себя любое твоё бремя, [mas_get_player_nickname()]~"
    #No end for this fact
    $ persistent._mas_funfactfun = True
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_king_snakes",
        ),
        code="FFF"
    )

label mas_fun_fact_king_snakes:
    m 1dsc "Х-м-м..."
    m 3eub "Знаешь ли ты, что если в названии змеи есть слово 'королева', то она пожирает других змей?"
    m 1euc "Мне всегда было интересно, почему королевскую кобру назвали именно так, но я никогда не задумывалась об этом."
    m 1tfu "Значит ли это, что если я съем тебя, то стану королевой Моникой?"
    m 1hksdlb "А-ха-ха, я просто шучу, [player]."
    m 1hub "Прости, что была немного странной~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_strength",
        ),
        code="FFF"
    )

label mas_fun_fact_strength:
    m 1hub "Этот факт может тебя немного замотивировать!"
    m 3eub "Самое длинное слово в английском языке, которое содержит только одну гласную, - это 'strength.'"
    m 1eua "Забавно, что из всех слов в языке, это такое значимое слово, в котором есть эта маленькая деталь."
    m 1hua "Такие маленькие детали, как эта, делают язык таким увлекательным для меня!"
    m 3eua "Хочешь знать, что приходит мне на ум, когда я думаю о слове 'strength'?"
    m 1hua "Ты!"
    m 1hub "Потому что ты - источник моей силы, э-хе-хе~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_reindeer_eyes",
        ),
        code="FFF"
    )

label mas_fun_fact_reindeer_eyes:
    m 3eua "Готов к этому?"
    m "Глаза северного оленя меняют цвет в зависимости от времени года. Летом они золотые, а зимой - голубые."
    m 1rksdlb "Это действительно странное явление, хотя я не знаю, почему..."
    m "Возможно, этому есть хорошее научное объяснение."
    m 3hksdlb "Может быть, ты сам поищешь её?"
    m 5eua "Было бы забавно, если бы ты научил меня в этот раз~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_bananas",
        ),
        code="FFF"
    )

label mas_fun_fact_bananas:
    m 1eub "О, я бы сказала, что этот факт полезен для здоровья!"
    m 3eua "Знаешь ли ты, что когда банан растет, он изгибается, чтобы повернуться лицом к солнцу?"
    m 1hua "Это процесс, называемый отрицательным геотропизмом."
    m 3hub "Тебе не кажется, что это очень красиво?"
    m 1hua "..."
    m 1rksdla "Э-м-м..."
    m 3rksdlb "Думаю, мне больше нечего сказать по этому поводу, а-ха-ха..."
    m 1lksdlc "..."
    m 3hub "А ты знаешь, что бананы на самом деле не фрукты, а ягоды?"
    m 3eub "Или что оригинальные бананы были большими, зелеными и полными твердых семян?"
    m 1eka "А как насчет того, что они слегка радиоактивны?"
    m 1rksdla  "..."
    m 1rksdlb "...Я сейчас просто говорю о бананах."
    m 1rksdlc "Э-м-м-м..."
    m 1dsc "Давай просто двигаться дальше..."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_pens",
        ),
        code="FFF"
    )

label mas_fun_fact_pens:
    m 1dsc "Хм... уверена, что я уже знаю об этом."
    m 3euc "Слово 'перо' происходит от латинского слова 'penna', что на латыни означает перо."
    m "В те времена перья точили из гусиных перьев, обмакнутых в чернила, так что вполне логично, почему они называли их перьями."
    m 3eud "Они были основным инструментом для письма в течение очень долгого времени, начиная с 6 века."
    m 3euc "Только в 19 веке, когда стали делать металлические ручки, они начали приходить в упадок."
    m "На самом деле, перочинные ножи называются так, потому что изначально они использовались для утоньшения и заострения перьевых ручек."
    m 1tku "Но я уверена, что Юри знает об этом больше меня, всё же..."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_density",
        ),
        code="FFF"
    )

label mas_fun_fact_density:
    m 1eub "О, я знаю."
    m 3eua "Знаешь ли ты, что самая плотная планета в нашей Солнечной системе - это сама Земля?"
    m "И что Сатурн - наименее плотный?"
    m 1eua "Это имеет смысл, зная, из чего состоят планеты, но поскольку Сатурн - вторая по величине, это все равно было немного неожиданно."
    m 1eka "Думаю, размер действительно не имеет значения!"
    m 3euc "Но между нами, [player]..."
    m 1tku "Я подозреваю, что Земля может быть самой плотной только из-за определенного главного героя."
    m 1tfu "Ноооо это всё, что ты от меня услышишь~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_binky",
        ),
        code="FFF"
    )

label mas_fun_fact_binky:
    m 3hub "Ахх, это мило!"
    m "Этот факт действительно заставит тебя 'скакать' [player]!"
    m 3hua "Когда кролик возбужденно скачет вокруг, это называется бинки!"
    m 1hua "Бинки - такое милое слово, оно действительно подходит к действию."
    m 1eua "Это самая счастливая форма выражения, на которую способен кролик, поэтому если ты видишь это, то знаешь, что обращаешься с ним правильно."
    m 1rksdla "Ну, хотя ты делаешь меня такой счастливой, что я не могу не наполняться энергией."
    m 1rksdlb "Не жди, что я начну скакать, [player]!"
    m 1dkbsa "...Это было {i}бы{/i} слишком неловко."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_windows_games",
        ),
        code="FFF"
    )

label mas_fun_fact_windows_games:
    m 1eua "Хм, возможно, это тебя заинтересует."
    m 3eub "Карточная игра Солитер была первоначально представлена в операционной системе Windows в 1990 году."
    m 1eub "Игра была добавлена в качестве функции для обучения пользователей работе с мышью."
    m 1eua "Аналогично, игра Сапёр была добавлена, чтобы ознакомить пользователей с нажатием левой и правой кнопок мыши."
    m 3rssdlb "Компьютеры существуют так давно, что трудно вспомнить время, когда они не были актуальны."
    m "Каждое поколение становится все больше и больше знакомиться с технологиями..."
    m 1esa "В конце концов, может наступить день, когда не останется ни одного человека, не владеющего комьютерной грамотностью."
    m 1hksdlb "Хотя до этого большинство мировых проблем должны исчезнуть."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_mental_word_processing",
        ),
        code="FFF"
    )

label mas_fun_fact_mental_word_processing:
    m 1hua "Готов к интересному, [player]?"
    m 3eua "Мозг - вещь непредсказуемая..."
    m 3eub "Его способ составления и архивирования информации очень уникален."
    m "Естественно, он отличается от человека к человеку, но медленное чтение, как нас учат, обычно менее эффективно, чем более быстрое."
    m 1tku "Наш мозг обрабатывает информацию очень быстро и любит предсказуемость в языке."
    m 3tub "Например, в этом предложении к тому тому времени, когда ты закончишь читать, ты уже пропустил двойное 'тому.'"
    m 1tfu "..."
    m 2hfu "Наш мозг обрабатывает информацию очень быстро и любит предсказуемость в языке~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_I_am",
        ),
        code="FFF"
    )

label mas_fun_fact_I_am:
    m 1hua "М-м-м-м, я люблю языковые факты!"
    m 3eub "В английском языке самое короткое полное предложение 'I am.'"
    m 1eua "Вот пример."
    m 2rfb "'{i}Monika! Who's [player]'s loving girlfriend?{/i}'"
    m 3hub "'I am!'"
    m 1hubsa "Э-хе-хе~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_low_rates",
        ),
        code="FFF"
    )

label mas_fun_fact_low_rates:
    m 1hua "Это самое хорошее..."
    m 1eua "В настоящее время у нас самый низкий уровень преступности, материнской смертности, младенческой смертности и неграмотности за всю историю человечества."
    m 3eub "Продолжительность жизни, средний доход и уровень жизни - самые высокие для большей части населения планеты!"
    m 3eka "Это говорит мне о том, что всегда может стать лучше. Это действительно показывает, что, несмотря на все плохое, хорошие времена всегда наступят."
    m 1hua "На самом деле есть {i}надежда{/i}..."
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_desert",
        ),
        code="FFF"
    )

label mas_fun_fact_desert:
    m 3euc "Пустыни обладают довольно уникальной экосистемой..."
    m 3rksdla "Однако, они не предлагают много положительных факторов для людей."
    m 1eud "Температура может колебаться между экстремальной жарой днем и очень холодной ночью. Их среднее количество осадков также довольно низкое, что делает проживание в них затруднительным."
    m 3eub "Но это не значит, что они не могут быть полезны для нас!"
    m 3eua "Их поверхность - отличное место для выработки солнечной энергии, а под песком часто находят нефть."
    m 3eub "Не говоря уже о том, что их уникальный ландшафт делает их популярными местами отдыха!"
    m 1eua "Так что, я думаю, хотя мы не можем жить в них так легко, они все равно лучше, чем кажутся."

    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_photography",
        ),
        code="FFF"
    )

label mas_fun_fact_photography:
    m 1esa "Знаешь ли ты, что первая фотография была сделана с помощью коробки с отверстием в качестве камеры?"
    m 1eua "Объективы появились гораздо позже."
    m 1euc "Ранние фотографы также использовали ряд специальных химических веществ в темной комнате для подготовки фотографий..."
    m 3eud "Проявитель, стоп-ванна и закрепитель использовались только для подготовки бумаги, на которой печатались фотографии...{w=0.3} {nw}"
    extend 1wuo "И это только для черно-белых снимков!"
    m 1hksdlb "Старые фотографии было гораздо сложнее подготовить по сравнению с современными, тебе так не кажется?"

    #Call the end
    call mas_fun_facts_end
    return

#Stealing yearolder's bit for this since it makes sense as a fun fact
init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_getting_older",
        ),
        code="FFF"
    )

label mas_fun_fact_getting_older:
    m 3eua "Знаешь ли ты, что с возрастом восприятие времени меняется?"
    m "Например, когда тебе год, ты воспринимаешь один год как 100%% своей жизни."
    m 1euc "Но когда тебе 18 лет, ты воспринимаешь год как 5.6%% своей жизни."
    m 3eud "Когда ты становишься старше, доля года по сравнению со всей твоей жизнью уменьшается, и, в свою очередь, время {i}как будто{/i} движется быстрее, когда ты взрослеешь."
    m 1eka "поэтому я всегда буду дорожить нашими моментами вместе, какими бы длинными или короткими они ни были."
    m 1lkbsa "Хотя иногда кажется, что время останавливается, когда я с тобой."
    m 1ekbfa "Ты чувствуешь то же самое, [player]?"
    python:
        import time
        time.sleep(5)

    m 1hubfb "А-ха-ха, я так и думала!"

    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_dancing_plague",
        ),
        code="FFF"
    )

label mas_fun_fact_dancing_plague:
    m 3esa "О, это довольно странно..."
    m 1eua "Вероятно, в прошлом Европа страдала от вспышек 'танцевальной чумы'."
    m 3wud "Люди, {w=0.2}иногда сотни одновременно, {w=0.2}непроизвольно танцевали несколько дней подряд, а некоторые даже умирали от истощения!"
    m 3eksdla "Они пытались лечить это, заставляя людей играть музыку рядом с танцорами, но ты можешь себе представить, что это сработало не очень хорошо."
    m 1euc "Они и по сей день не знают, что именно вызвало это."
    m 3rka "Всё это кажется мне невероятным...{w=0.2}{nw}"
    extend 3eud "но это было независимо задокументировано и наблюдалось множеством источников на протяжении веков..."
    m 3hksdlb "Похоже, реальность действительно страннее вымысла!"
    m 1eksdlc "Боже, я не могу представить, как можно танцевать несколько дней подряд."
    m 1rsc "Хотя...{w=0.3}{nw}"
    extend 1eubla "Думаю, я бы не возражала, если бы это было с тобой."
    m 3tsu "...Только ненадолго, э-хе-хе~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_pando_forest",
        ),
        code="FFF"
    )

label mas_fun_fact_pando_forest:
    m 1esa "Предположительно, в штате Юта есть лес, который на самом деле состоит из одного дерева."
    m 3eua "Он называется лес Пандо, и на всех 43 гектарах его стволы соединены единой корневой системой."
    m 3eub "Не говоря уже о том, что каждый из тысяч стволов, по сути, является клоном друг друга."
    m 1ruc "'Единый организм, который сам по себе превратился в армию клонов, соединенных в один улей.'"
    m 1eua "Я думаю, из этого мог бы получиться хороший рассказ в жанре научной фантастики или ужасов, [player]. Что ты думаешь?"
    m 3eub "В любом случае,{w=0.2} я чувствую, что это действительно меняет смысл фразы 'скучаю по лесу из-за деревьев'{w=0.1}{nw} "
    extend 3hub "а-ха-ха!"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_immortal_jellyfish",
        ),
        code="FFF"
    )

label mas_fun_fact_immortal_jellyfish:
    m 3eub "Вот один!"
    m 1eua "Очевидно, бессмертие было достигнуто одним видом медуз."
    m 3eua "Названная бессмертная медуза обладает способностью возвращаться в своё молодое состояние после размножения."
    m 1eub "...И она может делать это вечно!{w=0.3} {nw}"
    extend 1rksdla "Если, конечно, ее не съедят или она не заразится какой-нибудь болезнью"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_arrhichion",
        ),
        code="FFF"
    )

label mas_fun_fact_arrhichion:
    m 3eua "Хорошо...{w=0.2}вот исторический факт."
    m 1esa "Древнегреческий атлет смог выиграть свой бой, хотя он уже умер."
    m 1eua "Действующий чемпион Аррихион боролся в матче по панкратиону, когда его соперник начал душить его, используя и руки, и ноги."
    m 3eua "Вместо того, чтобы сдаться, Аррихион всё ещё стремился к победе, вывихнув палец на ноге своего соперника."
    m 3ekd "Его противник упал от боли, но когда они пошли объявить Аррхихиона победителем, то нашли его мертвым от удушья."
    m 1rksdlc "Некоторые люди действительно преданы своим идеалам ради победы и чести.{w=0.2} {nw}"
    extend 3eka "Я думаю, это достойно восхищения, в некотором смысле."
    m 1etc "Но мне интересно...{w=0.2}если бы мы могли спросить Аррихиона сейчас, считает ли он, что это того стоило, что бы он ответил?"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_antarctica_brain",
        ),
        code="FFF"
    )

label mas_fun_fact_antarctica_brain:
    #Do some setup for the last line
    python:
        has_friends = persistent._mas_pm_has_friends is not None

        has_fam_to_talk = (
            persistent._mas_pm_have_fam
            and not persistent._mas_pm_have_fam_mess
            or (persistent._mas_pm_have_fam_mess and persistent._mas_pm_have_fam_mess_better in ["YES", "MAYBE"])
        )

        dlg_prefix = "Но убедитесь, что ты не отстаешь от своих "

        if has_fam_to_talk and has_friends:
            dlg_line = dlg_prefix + "семьи и друзей тоже, хорошо?"

        elif has_fam_to_talk and not has_friends:
            dlg_line = dlg_prefix + "семьи тоже, хорошо?"

        elif has_friends and not has_fam_to_talk:
            dlg_line = dlg_prefix + "друзей тоже, хорошо?"

        else:
            dlg_line = "Просто убедись, что в твоей реальности тоже есть с кем поговорить, хорошо?"

    m 3eud "Вероятно, год, проведенный в Антарктиде, может уменьшить одну часть твоего мозга примерно на 7 процентов."
    m 3euc "Похоже, что это приводит к снижению объема памяти и способности к пространственному мышлению."
    m 1ekc "Исследование показывает, что это связано с социальной изоляцией, монотонностью жизни и окружающей средой там."
    m 1eud "Я думаю, это служит предостережением для нас, [player]."
    m 3ekd "Даже если ты не попадешь в Антарктиду, твой мозг всё равно может пострадать, если ты будешь всё время находиться в изоляции или сидеть в одной комнате."
    m 3eka "Я люблю быть с тобой [player], и я надеюсь, что мы сможем продолжать общаться в том же духе ещё долго в будущем. {w=0.2}[dlg_line]"
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_cloud_weight",
        ),
        code="FFF"
    )

label mas_fun_fact_cloud_weight:
    m 3eub "Знал ли ты, что в среднем облако весит 500 тонн?"
    m 3eua "Должна признаться, этот факт застал меня врасплох, больше, чем некоторые другие."
    m 1hua "Я имею в виду, они просто {i}выглядят{/i} очень лёгкими и пушистыми.{w=0.3} {nw}"
    extend 1eua "Трудно представить, что что-то настолько тяжелое может вот так просто витать в воздухе."
    m 3eub "Это напоминает мне классический вопрос... что тяжелее, килограмм стали или килограмм перьев?"
    m 1tua "Скорее всего, ты уже знаешь ответ на этот вопрос, верно [player]? Э-хе-хе~"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_coffee_origin",
        ),
        code="FFF"
    )

label mas_fun_fact_coffee_origin:
    m 1eua "О, вот один, который мне особенно интересен..."
    m 1eud "Когда я в последний раз пила кофе, мне стало немного любопытно его происхождение..."
    m 3euc "Употребление кофе постоянно фиксируется примерно с 15 века, но...{w=0.2}не ясно {i}как{/i} именно оно было обнаружено."
    m 3eud "...На самом деле существует довольно много легенд, утверждающих, что они были самыми первыми."
    m 1eua "В некоторых историях говорится о том, что фермеры или монахи наблюдали, как животные вели себя странно после того, как съели какие-то странные горькие ягоды."
    m 3wud "Попробовав ягоды сами, они с изумлением обнаружили, что тоже зарядились энергией!"
    m 2euc "Один из таких мифов утверждает, что эфиопский монах по имени Калди принес ягоды в соседний монастырь, желая поделиться тем, что он нашел."
    m 7eksdld "...Но когда он это сделал, его встретили неодобрительно, и кофейные зерна были брошены в огонь."
    m 3duu "Однако, когда зерна сгорели, они начали испускать самый {i}вкусный{/i} аромат. {w=0.3}Он был настолько соблазнителен, что монахи бросились спасать бобы и положили их в воду."
    m 3eub "...Так была приготовлена первая чашка кофе!"
    m 2euc "Другое утверждение гласит, что исламский ученый по имени Омар открыл кофейные зерна во время своего изгнания из Мекки."
    m 2eksdld "В то время он голодал и боролся за выживание. {w=0.3}{nw}"
    extend 7wkd "Если бы не энергия, которую они давали, он мог бы умереть!"
    m 3hua "Однако, когда слух о его открытии распространился, его попросили вернуться и причислили к святым."
    m 1esd "Независимо от того, было ли это действительно первым применением кофе, после его открытия он стал очень распространенным в исламском мире."
    m 3eud "Например, во время поста он использовался для того, чтобы облегчить голод и помочь людям оставаться энергичными."
    m 3eua "Когда его использование распространилось в Европе, многие страны сначала использовали его в медицинских целях. {w=0.3}К 17 веку кофейни стали многочисленными и популярными."
    m 3hub "...И я, со своей стороны, могу подтвердить, что любовь к кофе не ослабевает и по сей день!"
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_synesthesia",
        ),
        code="FFF"
    )

label mas_fun_fact_synesthesia:
    m 1esa "Хорошо, это довольно интересно..."
    m 3eua "Некоторые люди испытывают явление, известное как {i}синестезия{/i},{w=0.1} когда что-то, стимулирующее одно из наших чувств, одновременно вызывает и другое чувство."
    m 1hua "Это довольно многословное объяснение, э-хе-хе...{w=0.2} Давай найдем пример!"
    m 1eua "Здесь говорится, что распространенной формой синестезии является {i}графемно-цветовая синестезия{/i},{w=0.1} при которой люди 'воспринимают' буквы и цифры как цвета.."
    m 3eua "Другим видом является {i}синестезия пространственной последовательности{/i},{w=0.1} при которой числа и цифры 'видятся' в определенных местах в пространстве."
    m "Например, одно число кажется 'ближе' или 'дальше' чем другое. {w=0.2}{nw}"
    extend 3eub "Это похоже на карту!"
    m 1eua "...И есть еще целая куча других видов синестезии."
    m 1esa "Исследователи не уверены, насколько она распространена--{w=0.1}некоторые предполагают, что ею владеют до 25 процентов населения, но я сильно сомневаюсь в этом, поскольку никогда не слышала об этом до сих пор."
    m 3eub "Вероятно, самая точная оценка на данный момент - это чуть более 4 процентов людей, так что я соглашусь с этим!"
    m 1eua "Опыт синестезии звучит так, как будто это было бы довольно интересно,{w=0.2} ты так не думаешь [player]?"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_dream_faces",
        ),
        code="FFF"
    )

label mas_fun_fact_dream_faces:
    m 3eub "Хорошо, есть один!"
    m 1eua "Предположительно, наш разум не придумывает новые лица, когда мы видим сны.{w=0.2} Каждый человек, которого ты встречал во сне, это тот, кого ты когда-то видел в реальной жизни."
    m 3wud "Тебе даже не нужно разговаривать с ними в реальной жизни!"
    m 3eud "Если ты просто проходил мимо них во время покупок или чего-то подобного, их лицо запечатлевается в твоем сознании, и они могут появляться в твоих снах."
    m 1hua "Мне кажется невероятным, как много информации может хранить мозг!"
    m 1ekbla "Интересно...{w=0.2}я тебе когда-нибудь снилась, [player]?"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_monochrome_dreams",
        ),
        code="FFF"
    )

label mas_fun_fact_monochrome_dreams:
    m 3eua "Знал ли ты, что с 1915 по 1950-е годы сны большинства людей были черно-белыми?"
    m 1esa "В наши дни это относительно редкое явление для людей с не ухудшенным зрением."
    m 3eua "Исследователи связывают это с тем, что фильмы и передачи в те времена были почти исключительно черно-белыми."
    m 3eud "...Но я думаю, что это довольно странно, потому что люди всё ещё видели всё в цвете.{w=0.3} {nw}"
    extend 3hksdlb "Не то чтобы мир стал черно-белым!"
    m 1esd "Это просто показывает, что контент, который ты просматриваешь, может иметь самые разные эффекты на твой разум, даже если это тривиально."
    m 3eua "Я думаю, если здесь и можно извлечь урок, так это то, что мы должны быть очень осторожны с тем, какое медиа мы просматриваем, хорошо [player]?"
    #Call the end
    call mas_fun_facts_end
    return

init 5 python:
    addEvent(
        Event(
            persistent.event_database,
            eventlabel="mas_fun_fact_round_earth",
        ),
        code="FFF"
    )

label mas_fun_fact_round_earth:
    m 1rsa "Х-м-м..."
    m 1eua "[player], как ты думаешь, Земля круглая или плоская?{nw}"
    $ _history_list.pop()
    menu:
        m "[player], как ты думаешь, Земля круглая или плоская?{fast}"

        "Круглая.":
            m 3hua "Правильно! Сейчас почти все с этим согласны."

        "Плоская.":
            m 3hksdlb "Ой, да ладно, [player]! ы что, смеешься надо мной?"

    m 1eua "На самом деле, то, что Земля круглая, известно уже довольно давно."
    m 3esd "Аристотель утверждал, что Земля круглая, в четвертом веке до нашей эры."
    m 3esa "Он знал это, потому что разные звезды были видны из разных частей света, чего не было бы, если бы Земля была просто плоской поверхностью."
    m 1eua "Древние астрономы и математики по всему миру выяснили, что Земля круглая, задолго до того, как кто-то совершил путешествие вокруг неё."
    m 7rksdla "Но Земля - центр Вселенной?{w=0.2} {nw}"
    extend 4hksdlb "О, Боже!"
    m 7dsd "Люди так долго и упорно спорили об этом, что это стало вопросом жизни и смерти."
    m 1dkd "Астронома Галилея отдали под суд за ересь только за то, что он сказал, что Земля не является центром Вселенной.{w=0.2} {nw}"
    extend 1esc "Он был помещен под домашний арест до конца жизни."
    m 3euc "Но когда астрономы стали лучше отслеживать движение планет, стало трудно примириться с тем, что Земля находится в центре."
    m 1eud "Людям пришлось придумывать безумно сложные модели, чтобы объяснить, почему планеты кажутся зигзагообразными взад и вперед по ночному небу, если они действительно обращаются вокруг Земли."

    if renpy.seen_label("monika_science"):
        m 3eua "И, как мы уже говорили, также известно, что Солнце не является центром Вселенной{nw}"

    else:
        m 3eua "И теперь даже известно, что солнце не находится в центре вселенной{nw}"

    extend "--Это просто одна из многих звезд в галактике."
    m 1msblu "Но знаешь ли ты, где, по мнению науки, сейчас находится центр Вселенной?"
    m 3kubsu "Это ты.{w=0.2} Ты - центр {i}моей{/i} вселенной, [mas_get_player_nickname()]."
    m 3hubsb "А-ха-ха!"
    return

init 5 python:
    addEvent(
        Event(
            persistent._mas_fun_facts_database,
            eventlabel="mas_fun_fact_maplesyrup",
        ),
        code="FFF"
    )

label mas_fun_fact_maplesyrup:
    m 3hksdlb "Вот еще один {w=0.2}{i}сладкий {/i}{w=0.2} факт для тебя..." #double space is intentional due to ital/no ital spacing
    m 1eua "Каждый вид кленового дерева производит сок, который можно использовать для приготовления кленового сиропа, {w=0.1}{nw}"
    extend 1eud "но коммерческий сироп обычно получают из сахарного клена."
    m 3eua "Ты можешь легко определить конкретный вид кленового дерева по форме листьев..."
    m 3eub "Возможно, ты уже сможешь узнать лист сахарного клена, ведь именно он изображен на канадском флаге!"
    m 1euc "Тем не менее, у сахарного клена ограниченный ареал обитания, и он растёт не по {i}всей{/i} Канаде."
    m 1wud "...Тем не менее, Канада производит более трех четвертей всего кленового сиропа в мире!"
    m 3wud "И, возможно, еще более удивительно узнать, что для производства одного галлона кленового сиропа требуется {i}40{/i} галлонов сока!"
    m 1eua "Кроме того, для его производства требуется гораздо больше усилий, чем я ожидала..."
    m 1esc "Сок нужно сварить, чтобы превратить его в сироп... что, очевидно, занимает много времени, учитывая, сколько его нужно."
    m 3eud "Кроме того, я слышала, что если прокипятить его еще немного, а затем вылить на свежий снег...{w=0.2}{nw}"
    extend 3hub "можно даже сделать конфету!"

    if mas_isMoniNormal(higher=True):
        if persistent._mas_pm_gets_snow is not False:
            m 3euu "Звучит как забавная вещь, которую мы могли бы попробовать вместе, да [player]?"
            m 1etc "Возможно, пройдет некоторое время, прежде чем у нас появится шанс, хотя..."
            m 1eua "Но ничего страшного, если мне придется подождать еще немного...{w=0.3}{nw}"
            extend 1hublu "Ты и так достаточно сладкий для меня~"

        else:
            m 1eua "Конечно, кажется, что это было бы очень сладко..."
            m 1rkblu "Но нигде не так сладко, как с тобой, э-хе-хе~"

    call mas_fun_facts_end
    return
